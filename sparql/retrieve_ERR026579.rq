# Retrieve statistics of SRA entry ERR026579 from the Qunato database

PREFIX sos: <http://purl.jp/bio/10/quanto/ontology/sos#>
PREFIX quanto: <http://purl.jp/bio/10/quanto/resource/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX pav: <http://purl.org/pav/>
PREFIX sio: <http://semanticscience.org/resource/>

SELECT
  ?quanto
  ?quanto_id
  ?encoding
  ?file_type
  ?fastqc_version
  ?min_seq_len
  ?median_seq_len
  ?max_seq_len
  ?mean_bc_quality
  ?median_bc_quality
  ?n_content
  ?gc_content
  ?total_seq
FROM <http://quanto.dbcls.jp>
WHERE {
  ?quanto a sos:SequenceStatisticsReport;
    rdfs:seeAlso <http://identifiers.org/insdc.sra/ERR026579>;
    dct:identifier ?quanto_id;
    sos:fastqcVersion ?fastqc_version;
    sos:encoding ?encoding;
    sos:fileType ?file_type;
    sio:SIO_000216 [
       a sos:MinimumSequenceLength;
         sio:SIO_000300 ?min_seq_len;
     ];
     sio:SIO_000216 [
       a sos:MedianSequenceLength;
         sio:SIO_000300 ?median_seq_len;
     ];
     sio:SIO_000216 [
       a sos:MaximumSequenceLength;
         sio:SIO_000300 ?max_seq_len;
     ];
     sio:SIO_000216 [
       a sos:MeanBaseCallQuality;
         sio:SIO_000300 ?mean_bc_quality;
     ];
     sio:SIO_000216 [
       a sos:MedianBaseCallQuality;
         sio:SIO_000300 ?median_bc_quality;
     ];
     sio:SIO_000216 [
       a sos:NContent;
         sio:SIO_000300 ?n_content;
     ];
     sio:SIO_000216 [
       a sos:PercentGC;
         sio:SIO_000300 ?gc_content;
     ];
     sio:SIO_000216 [
       a sos:TotalSequences;
         sio:SIO_000300 ?total_seq;
     ] .
}
