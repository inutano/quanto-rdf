# Retrieve top 50 high-throughput sequencing runs with filtering by base call quality and read length
PREFIX sos: <http://purl.jp/bio/10/quanto/ontology/sos#>
PREFIX quanto: <http://purl.jp/bio/10/quanto/resource/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX pav: <http://purl.org/pav/>
PREFIX sio: <http://semanticscience.org/resource/>

SELECT ?id ?insdcId ?median_bc_quality ?total_sequences ?median_seq_length
FROM <http://quanto.dbcls.jp>
WHERE {
  ?quanto dct:identifier ?id;
  rdfs:seeAlso ?insdcId;
  sio:SIO_000216 [
    a sos:TotalSequences;
      sio:SIO_000300 ?total_sequences;
  ];
  sio:SIO_000216 [
    a sos:MedianBaseCallQuality;
      sio:SIO_000300 ?median_bc_quality;
  ];
  sio:SIO_000216 [
    a sos:MedianSequenceLength;
      sio:SIO_000300 ?median_seq_length;
  ].

  FILTER(?median_bc_quality > 30).
  FILTER(?median_seq_length > 70).
}
ORDER BY DESC(?total_sequences)
LIMIT 50
